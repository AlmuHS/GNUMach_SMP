dnl Configure fragment for i386.

dnl Copyright (C) 1999, 2004, 2006, 2007, 2008 Free Software Foundation, Inc.

dnl Permission to use, copy, modify and distribute this software and its
dnl documentation is hereby granted, provided that both the copyright
dnl notice and this permission notice appear in all copies of the
dnl software, derivative works or modified versions, and any portions
dnl thereof, and that both notices appear in supporting documentation.
dnl
dnl THE FREE SOFTWARE FOUNDATION ALLOWS FREE USE OF THIS SOFTWARE IN ITS
dnl "AS IS" CONDITION.  THE FREE SOFTWARE FOUNDATION DISCLAIMS ANY
dnl LIABILITY OF ANY KIND FOR ANY DAMAGES WHATSOEVER RESULTING FROM THE
dnl USE OF THIS SOFTWARE.

#
# Definitions.
#

[case $host_cpu in
  i?86)]
    AM_CONDITIONAL([HOST_ix86], [true])

    # Some of the i386-specific code checks for these.
    AC_DEFINE([__ELF__], [1], [__ELF__])

    # Determines the size of the CPU cache line.
    AC_DEFINE([CPU_L1_SHIFT], [6], [CPU_L1_SHIFT])

    [# Does the architecture provide machine-specific interfaces?
    mach_machine_routines=1;;
  *)]
    AM_CONDITIONAL([HOST_ix86], [false])[;;
esac

case $host_platform in
  at)]
    AM_CONDITIONAL([PLATFORM_at], [true])[;;
  *)]
    AM_CONDITIONAL([PLATFORM_at], [false])[;;
esac

#
# Formerly in `i386/bogus/'.
#

case $host_platform:$host_cpu in
  at:i?86)
    # should be 4, but we do not support shared IRQ for these
    ncom=2
    nlpr=1

    # i386/bogus/platforms.h]
    AC_DEFINE([AT386], [1], [AT386])[;;
  xen:i?86)
    # TODO.  That should probably not be needed.
    ncom=1
    # TODO.  That should probably not be needed.
    # i386/bogus/platforms.h]
    AC_DEFINE([AT386], [1], [AT386])[;;
  *)
    :;;
esac]

#
# Options.
#

# The immediate console, useful for debugging early system
# initialization.  Disabled by default.
AC_DEFINE([ENABLE_IMMEDIATE_CONSOLE], [0], [ENABLE_IMMEDIATE_CONSOLE])

AC_ARG_ENABLE([lpr],
  AS_HELP_STRING([--enable-lpr], [lpr device; on ix86-at enabled by default]))
[case $host_platform:$host_cpu in
  at:i?86)
    case $enable_device_drivers in
      default)
        enable_lpr=${enable_lpr-yes};;
      *)
        enable_lpr=${enable_lpr-no};;
    esac;;
  *)
    if [ x"$enable_lpr" = xyes ]; then]
      AC_MSG_ERROR([cannot enable `lpr' in this configuration.])
    [fi;;
esac
if [ x"$enable_lpr" = xyes ]; then]
  AC_DEFINE([MACH_LPR], [], [lpr device])
  AM_CONDITIONAL([enable_lpr], [true])
[else]
  AM_CONDITIONAL([enable_lpr], [false])
[fi]

AC_ARG_ENABLE([apic],
  AS_HELP_STRING([--enable-apic], [LAPIC/IOAPIC support]))

[if [ $mach_ncpus -gt 1 ]; then]
  # For SMP we need apic
  enable_apic=yes
[fi]

[if [ x"$enable_apic" = xyes ]; then]
  AC_DEFINE([APIC], [1], [APIC support])
  AM_CONDITIONAL([enable_apic], [true])
[else]
  AM_CONDITIONAL([enable_apic], [false])
[fi]

[case $host_platform:$host_cpu in
  xen:i?86)
    enable_pae=${enable_pae-yes};;
  *:i?86)
    :;;
  *:x86_64)
    enable_pae=${enable_pae-yes};;
  *)
    if [ x"$enable_pae" = xyes ]; then]
      AC_MSG_ERROR([can only enable the `PAE' feature on ix86.])
    [fi;;
esac]

AC_ARG_WITH([_START_MAP],
  AS_HELP_STRING([--with-_START_MAP=0x1000000], [specify kernel mapping start address]),
  [_START_MAP="$withval"], [_START_MAP=0x1000000])
AC_SUBST(_START_MAP)

dnl Local Variables:
dnl mode: autoconf
dnl End:
